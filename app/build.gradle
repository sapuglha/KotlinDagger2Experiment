plugins {
    id 'com.gladed.androidgitversion' version '0.4.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

androidGitVersion {
    codeFormat 'MNNPPP'
    format '%tag%%-count%%-commit%%-branch%'
    hideBranches = [ 'develop', 'master' ]
}

def vName = androidGitVersion.name()
def vCode = androidGitVersion.code()

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.robotsandpencils.kotlindaggerexperiment"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        // See: https://github.com/gladed/gradle-android-git-version.
        versionName vName
        versionCode vCode

        testInstrumentationRunner "com.robotsandpencils.kotlindaggerexperiment.TestRunner"

        // Output kotlin-dagger-experiment-flavor.apk instead of app-flavor.apk
        archivesBaseName = "kotlin-dagger-experiment"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

configurations.all {
    resolutionStrategy.force "com.google.dagger:dagger:$daggerVersion"
    resolutionStrategy.force "com.squareup.okhttp3:okhttp:$okhttpVersion"
    resolutionStrategy.force "junit:junit:4.12"
    resolutionStrategy.force "com.google.code.findbugs:jsr305:3.0.1"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(":data")
    implementation project(":domain")

    implementation "com.google.android.material:material:$materialComponentsVersion"

    implementation rootProject.ext.androidX_appCompat
    implementation rootProject.ext.androidX_browser
    implementation rootProject.ext.androidX_cardView
    implementation rootProject.ext.androidX_constraintLayout

    // KTX
    implementation rootProject.ext.androidX_core_ktx

    implementation rootProject.ext.kotlinStdlib
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.18"

    // Dagger 2 and Dagger Android
    implementation rootProject.ext.dagger
    implementation rootProject.ext.dagger_android
    implementation rootProject.ext.dagger_android_support
    kapt rootProject.ext.dagger_compiler
    kapt rootProject.ext.dagger_android_processor

    // RXJava
    implementation rootProject.ext.rxJava
    implementation rootProject.ext.rxJava_rxkotlin
    implementation rootProject.ext.rxJava_rxandroid
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    implementation 'com.mtramin:rxfingerprint:2.1.0'

    // Architecture Components
    implementation rootProject.ext.androidX_lifeCycle
    implementation rootProject.ext.androidX_lifeCycle_extensions
    implementation rootProject.ext.androidX_room
    implementation rootProject.ext.androidX_room_rx
    kapt rootProject.ext.androidX_lifeCycle_compiler
    kapt rootProject.ext.androidX_room_compiler
    testImplementation rootProject.ext.androidX_room_testing

    // Groupie
    implementation "com.xwray:groupie:$groupieVersion"

    // Logging
    implementation 'com.github.ajalt:timberkt:1.3.0'

    // Secure preferences
    implementation 'com.scottyab:secure-preferences-lib:0.1.4'

    // Retrofit 2
    implementation rootProject.ext.retrofit
    implementation rootProject.ext.retrofitRxJava2Adapter
    implementation rootProject.ext.retrofitGsonConverter
    implementation rootProject.ext.gson
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    // Image loading
    implementation rootProject.ext.picasso

    // Arrow
    implementation "io.arrow-kt:arrow-core:$arrowVersion"

    // Material Dialogs
    implementation 'com.afollestad.material-dialogs:core:0.9.4.2'
    implementation 'com.afollestad.material-dialogs:commons:0.9.4.2'

    // Date classes
    implementation 'net.danlew:android.joda:2.9.9.2'

    // Android Tests
    androidTestImplementation rootProject.ext.androidx_test_runner
    androidTestImplementation rootProject.ext.androidx_test_rules
    androidTestImplementation(rootProject.ext.androidx_test_espresso_core, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation rootProject.ext.androidx_test_espresso_contrib

    androidTestImplementation("com.squareup.okhttp3:mockwebserver:$okhttpVersion") {
        exclude group: 'org.bouncycastle'
    }
    androidTestImplementation 'com.squareup.rx.idler:rx2-idler:0.9.0'
    androidTestImplementation(rootProject.ext.mockito_kotlin2) {
        exclude group: 'org.jetbrains.kotlin'
    }
    androidTestImplementation rootProject.ext.mockito_core
    androidTestImplementation rootProject.ext.mockito_android

    kaptAndroidTest rootProject.ext.dagger_compiler
    kaptAndroidTest rootProject.ext.dagger_android_processor

    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    androidTestImplementation rootProject.ext.androidx_test_uiautomator
    androidTestImplementation 'com.github.tmurakami:dexopener:0.12.1'

    // Unit Tests
    testImplementation rootProject.ext.junit
    testImplementation 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testImplementation rootProject.ext.mockito_core
    testImplementation(rootProject.ext.mockito_kotlin2) {
        exclude group: 'org.jetbrains.kotlin'
    }

    testImplementation rootProject.ext.androidX_core_testing
}
